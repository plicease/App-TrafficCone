#!/usr/bin/perl

use strict;
use warnings;
use File::Spec;
use File::HomeDir;
use YAML qw( LoadFile DumpFile );
use Mojo::URL;

# PODNAME: traffic_cone
# ABSTRACT: Web server that just returns 503 Service unavailable
# VERSION

my $config = do {
  my $config_file_name = File::Spec->catfile(File::HomeDir->my_home, '.traffic_cone.yml');
  unless(-e $config_file_name)
  {
    DumpFile($config_file_name, {
      listen => 'http://*:3000',
      message => '503 Site down for maintenance',
    });
  }
  LoadFile($config_file_name);
};

local $ENV{TRAFFIC_CONE} = $config->{message};
local $ENV{MOJO_LISTEN}  = do {
  my $url = Mojo::URL->new($config->{listen});
  for (qw( ca cert key verify )) {
    $url->query->param( $_ => $config->{"ssl_$_"} )
      if defined $config->{"ssl_$_"};
  }
  $url->to_string;
};

@ARGV = qw( daemon ) unless @ARGV > 0;

require Mojolicious::Commands;
Mojolicious::Commands->start_app('App::TrafficCone');
